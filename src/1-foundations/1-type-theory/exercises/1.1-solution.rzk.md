# Solution to exercise 1.1

This is a literate Rzk file:

```rzk
#lang rzk-1
```

Given functions $f : A \to B$ and $g : B \to C$,
define their composite $g \circ f : A \to C$.
Show that we have $h \circ (g \circ f) \equiv (h \circ g) \circ f$.

## Solution

```rzk
#define compose
  (A B C : U)
  (g : B → C)
  (f : A → B)
  : A → C
  := \ x → g (f x)
```

Associativity is automatic (by `refl`):

```rzk
#def compose_associativity
  (A B C D : U)
  (f : A -> B)
  (g : B -> C)
  (h : C -> D)
  : compose A C D h (compose A B C g f) = compose A B D (compose B C D h g) f
  := refl
```

An explicit proof (not checked by `rzk`):

```rzk
-- compose A C D h (compose A B C g f)
-- = \ x → h ((compose A B C g f) x)       (unfolding compose)
-- = \ x → h ((\ x2 → g (f x2)) x)         (unfolding compose)
-- = \ x → h (g (f x))                     (beta)
-- = \ x → (\ x2 → h (g x2)) (f x)         (beta)
-- = \ x → (compose B C D h g) (f x)       (fold compose)
-- = compose A B D (compose B C D h g) f   (fold compose)
```
