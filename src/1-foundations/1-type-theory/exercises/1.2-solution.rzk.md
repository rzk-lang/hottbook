# Solution to exercise 1.2

This is a literate Rzk file:

```rzk
#lang rzk-1
```

## Problem statement

Derive the recursion principle for products
$\mathsf{rec}_{A \times B}$ using only the projections,
and verify that the definitional equalities are valid.
Do the same for $\Sigma$-types.

## Solution for products

In this solution, we use `prod`, defined earlier in [chapter 1.5](../05-product-types.rzk.md).

Introducing constructor for values of `prod` type:
```rzk
#def pair
  (A B : U)
  : A -> B -> prod A B
  := \ a b -> (a, b)
```

Projections were also defined in [chapter 1.5](../05-product-types.rzk.md) (`pr₁`, `pr₂`).

Type of the recursor on $A \times B$ is 

$$
\mathsf{rec}_{A \times B}: \prod_{C:\mathcal{U}} (A \rightarrow B \rightarrow C) \rightarrow A \times B \rightarrow C
$$

Defining it through projections:
```rzk
#def prod-rec-via-projections
  (A B : U)
  (C : U)
  : (g : A -> B -> C) -> (p : prod A B) -> C
  := \ g p -> g (pr₁-via-rec A B p) (pr₂-via-rec A B p)
```

Definitions of projections via recursion in the other direction were already provided in [chapter 1.5](../05-product-types.rzk.md) (`pr₁-via-rec`, `pr₂-via-rec`)

### Checking definitional equalities

Recursion from projections:
```rzk
#def prod-rec-via-projections-works
  (A B C : U)
  (g : A -> B -> C)
  (a : A)
  (b : B)
  : prod-rec-via-projections A B C g (pair A B a b) = g a b
  := refl
```
Projections from the recursion:
```rzk
#def pr₁-via-rec-works
  (A B : U)
  (a : A)
  (b : B)
  : pr₁-via-rec A B (pair A B a b) = a
  := refl

#def pr₂-from-rec-works
  (A B : U)
  (a : A)
  (b : B)
  : pr₂-via-rec A B (pair A B a b) = b
  := refl
```

## Solution for dependant pairs

Sigma types are built-in. Also, `pr₁-sigma`, `pr₂-sigma`, `rec-sigma`, `ind-sigma` were already defined in [chapter 1.6](../06-dependent-pair-types.rzk).

Defining recursion from `pr₁-sigma` and `pr₂-sigma`:
```rzk
#def rec-sigma-via-projections
  (A : U)
  (B : A -> U)
  (C : U)
  : (g : (x : A) -> B x -> C) -> (p : Sigma (x : A), (B x)) -> C
  := \ g p -> g (pr₁-sigma A B p) (pr₂-sigma A B p)
```

(Extra, not in the task) Other direction of definition (projecitons via recursion):

```
#def pr₁-sigma-via-rec
  (A : U)
  (B : A -> U)
  : (Sigma (x : A), (B x)) -> A
  := \p -> rec-sigma A B A (\ x y -> x) p
```

For the second projection ($\mathsf{pr}_2$) we need the induction, since the output type depends on $x : A$:
```rzk
#def pr₂-sigma-via-rec
  (A : U)
  (B : A -> U)
  : (p : Sigma (x : A), (B x)) -> (B (pr₁-sigma-via-rec A B p))
  := \p -> ind-sigma A B (\ p1 -> B (pr₁-sigma-via-rec A B p1)) (\ x y -> y) p
```

### Checking the definitional equalities

```rzk
#def rec-sigma-via-projections-works
  (A : U)
  (B : A -> U)
  (C : U)
  (g : (x : A) -> B x -> C)
  (a : A)
  (b : B a)
  : rec-sigma-via-projections A B C g (a, b) = g a b
  := refl

#def pr₁-sigma-via-rec-works
  (A : U)
  (B : A -> U)
  (a : A)
  (b : B a)
  : pr₁-sigma-via-rec A B (a, b) = a
  := refl

#def pr₂-sigma-via-rec-works
  (A : U)
  (B : A -> U)
  (a : A)
  (b : B a)
  : pr₂-sigma-via-rec A B (a, b) = b
  := refl
```
