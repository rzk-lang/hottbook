# Solution to exercise 1.2

This is a literate Rzk file:

```rzk
#lang rzk-1
```

Derive the recursion principle for products
$rec_{A \times B}$ using only the projections,
and verify that the definitional equalities are valid.
Do the same for $\Sigma$-types.

## Solution

### Product $A \times B$.

Introducing definitions
```rzk
-- defined before

-- #def id
--   (A : U)
--   : A -> A
--   := \ a -> a

-- #def prod
--   (A B : U)
--   : U
--   := Sigma (_ : A), B

#def pair
  (A B : U)
  : A -> B -> prod A B
  := \ a b -> (a, b)
```

Defining recursion on $A \times B$ through projections
```rzk
#def pr_1
  (A B : U)
  : prod A B -> A
  := \(a, b) -> a

#def pr_2
  (A B : U)
  : prod A B -> B
  := \(a, b) -> b

-- rec_AxB((C: U), (g: A -> B -> C), (p: AxB)) 
#def rec_prod
  (A B : U)
  (C : U)
  : (g : A -> B -> C) -> (p : prod A B) -> C
  := \ g p -> g (pr_1 A B p) (pr_2 A B p)
```

Defining projections through a recursion on $A \times B$
```rzk
-- other way
#def rec_prod_reversed
  (A B : U)
  (C : U)
  : (g : A -> B -> C) -> (p : prod A B) -> C
  := \ g (a, b) -> g a b

#def pr_1_reversed
  (A B : U)
  : prod A B -> A
  := rec_prod_reversed A B A (\a b -> a)

#def pr_2_reversed
  (A B : U)
  : prod A B -> B
  := rec_prod_reversed A B B (\a b -> b)
```

Checking definitional equalities
```rzk
-- recursion from projections

#def rec_from_pr_works
  (A B C : U)
  (g : A -> B -> C)
  (a : A)
  (b : B)
  : rec_prod A B C g (pair A B a b) = g a b
  := refl

-- projections from recursion

#def pr_1_from_rec_works
  (A B : U)
  (a : A)
  (b : B)
  : pr_1 A B (pair A B a b) = a
  := refl

#def pr_2_from_rec_works
  (A B : U)
  (a : A)
  (b : B)
  : pr_2 A B (pair A B a b) = b
  := refl
```

###  Dependant pairs

Sigma types are built-in, so we head right into the exercise.

Defining projections and recursion
```rzk
#def pr_1_sigma
  (A : U)
  (B : A -> U)
  : (Sigma (x : A), (B x)) -> A
  := \p -> first p

#def pr_2_sigma
  (A : U)
  (B : A -> U)
  : (p : Sigma (x : A), (B x)) -> (B (first p))
  := \p -> second p

-- rec_{Sigma (x : A) (B x)} ((C: U), (g: (x : A) -> B x -> C), (p: Sigma (x : A) (B x)))
#def rec_sigma
  (A : U)
  (B : A -> U)
  (C : U)
  : (g : (x : A) -> B x -> C) -> (p : Sigma (x : A), (B x)) -> C
  := \ g p -> g (pr_1_sigma A B p) (pr_2_sigma A B p)
```

(Extra, not in the task) Other way around:
```rzk
-- rec_{Sigma (x : A) (B x)} ((C: U), (g: (x : A) -> B x -> C), (p: Sigma (x : A) (B x)))
#def rec_sigma_reversed
  (A : U)
  (B : A -> U)
  (C : U)
  : (g : (x : A) -> B x -> C) -> (p : Sigma (x : A), (B x)) -> C
  := \ g (a, b) -> g a b

#def pr_1_sigma_reversed
  (A : U)
  (B : A -> U)
  : (Sigma (x : A), (B x)) -> A
  := \p -> rec_sigma_reversed A B A (\ x y -> x) p

#def ind_sigma_reversed
  (A : U)
  (B : A -> U)
  (C : (Sigma (x : A), (B x)) -> U)
  : (g : (x : A) -> (y : B x) -> C (x, y)) -> (p : Sigma (x : A), (B x)) -> C p
  := \ g (a, b) -> g a b

-- doesn't work for now
#def pr_2_sigma_reversed
  (A : U)
  (B : A -> U)
  : (p : Sigma (x : A), (B x)) -> (B (pr_1_sigma_reversed A B p))
  := \p -> ind_sigma_reversed A B (\ p1 -> B (pr_1_sigma_reversed A B p1)) (\ x y -> y) p

```

Checking the definitional equality

```rzk
#def rec_sigma_works
  (A : U)
  (B : A -> U)
  (C : U)
  (g : (x : A) -> B x -> C)
  (a : A)
  (b : B a)
  : rec_sigma A B C g (a, b) = g a b
  := refl
```
