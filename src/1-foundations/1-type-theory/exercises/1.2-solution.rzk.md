# Solution to exercise 1.2

This is a literate Rzk file:

```rzk
#lang rzk-1
```

## Problem statement

Derive the recursion principle for products
$\mathsf{rec}_{A \times B}$ using only the projections,
and verify that the definitional equalities are valid.
Do the same for $\Sigma$-types.

## Solution for products

In this solution, we use `prod`, defined earlier in [chapter 1.5](../05-product-types.rzk.md).

Introducing constructor for values of `prod` type:
```rzk
#def pair
  (A B : U)
  : A -> B -> prod A B
  := \ a b -> (a, b)
```

Projections were also defined in [chapter 1.5](../05-product-types.rzk.md) (`pr₁`, `pr₂`).

Type of the recursor on $A \times B$ is 

$$
\mathsf{rec}_{A \times B}: \prod_{C:\mathcal{U}} (A \rightarrow B \rightarrow C) \rightarrow A \times B \rightarrow C
$$

Defining it through projections:
```rzk
#def prod-rec-via-projections
  (A B : U)
  (C : U)
  : (g : A -> B -> C) -> (p : prod A B) -> C
  := \ g p -> g (pr₁-via-rec A B p) (pr₂-via-rec A B p)
```

Definitions of projections via recursion in the other direction were already provided in [chapter 1.5](../05-product-types.rzk.md) (`pr₁-via-rec`, `pr₂-via-rec`)

### Checking definitional equalities

Recursion from projections:
```rzk
#def prod-rec-via-projections-works
  (A B C : U)
  (g : A -> B -> C)
  (a : A)
  (b : B)
  : prod-rec-via-projections A B C g (pair A B a b) = g a b
  := refl
```

!!! warning

    TODO: Write down the explicit proof (in latex/rzk syntax)


Projections from the recursion:
```rzk
#def pr₁-via-rec-works
  (A B : U)
  (a : A)
  (b : B)
  : pr₁-via-rec A B (pair A B a b) = a
  := refl

#def pr₂-from-rec-works
  (A B : U)
  (a : A)
  (b : B)
  : pr₂-via-rec A B (pair A B a b) = b
  := refl
```

!!! warning

    TODO: Write down the explicit proofs (in latex/rzk syntax)

## Solution for dependant pairs

$\Sigma$-types are built-in. Also, `pr₁-Σ`, `pr₂-Σ`, `rec-Σ`, `ind-Σ` were already defined in [chapter 1.6](../06-dependent-pair-types.rzk.md).

Defining recursion from `pr₁-Σ` and `pr₂-Σ`:
```rzk
#def rec-Σ-via-projections
  (A : U)
  (B : A -> U)
  (C : U)
  : (g : (x : A) -> B x -> C) -> (p : Σ (x : A), B x) -> C
  := \ g p -> g (pr₁-Σ A B p) (pr₂-Σ A B p)
```

(Extra, not in the task) Other direction of definition (projecitons via recursion):

```rzk
#def pr₁-Σ-via-rec
  (A : U)
  (B : A -> U)
  : (Σ (x : A), B x) -> A
  := \p -> rec-Σ A B A (\ x y -> x) p
```

For the second projection ($\mathsf{pr}_2$) we need the induction, since the output type depends on $x : A$:
```rzk
#def pr₂-Σ-via-rec
  (A : U)
  (B : A -> U)
  : (p : Σ (x : A), B x) -> (B (pr₁-Σ-via-rec A B p))
  := \p -> ind-Σ A B (\ p1 -> B (pr₁-Σ-via-rec A B p1)) (\ x y -> y) p
```

### Checking the definitional equalities

```rzk
#def rec-Σ-via-projections-works
  (A : U)
  (B : A -> U)
  (C : U)
  (g : (x : A) -> B x -> C)
  (a : A)
  (b : B a)
  : rec-Σ-via-projections A B C g (a, b) = g a b
  := refl

#def pr₁-Σ-via-rec-works
  (A : U)
  (B : A -> U)
  (a : A)
  (b : B a)
  : pr₁-Σ-via-rec A B (a, b) = a
  := refl

#def pr₂-Σ-via-rec-works
  (A : U)
  (B : A -> U)
  (a : A)
  (b : B a)
  : pr₂-Σ-via-rec A B (a, b) = b
  := refl
```

!!! warning

    TODO: Write down the explicit proofs (in latex/rzk syntax)
